-- Create patients table
CREATE TABLE patients (
    patient_id NUMBER PRIMARY KEY,
    name       VARCHAR2(100),
    age        NUMBER,
    gender     VARCHAR2(10),
    admitted   CHAR(1) DEFAULT 'N'
);

-- Create doctors table
CREATE TABLE doctors (
    doctor_id NUMBER PRIMARY KEY,
    name      VARCHAR2(100),
    specialty VARCHAR2(50)
);
-- Package specification
CREATE OR REPLACE PACKAGE hospital_pkg AS

    TYPE patient_rec IS RECORD (
        patient_id patients.patient_id%TYPE,
        name       patients.name%TYPE,
        age        patients.age%TYPE,
        gender     patients.gender%TYPE,
        admitted   patients.admitted%TYPE
    );

    TYPE patient_table IS TABLE OF patient_rec;

    PROCEDURE bulk_load_patients(p_list IN patient_table);
    FUNCTION show_all_patients RETURN SYS_REFCURSOR;
    FUNCTION count_admitted RETURN NUMBER;
    PROCEDURE admit_patient(p_id IN NUMBER);

END hospital_pkg;
/

-- Package body
CREATE OR REPLACE PACKAGE BODY hospital_pkg AS

    PROCEDURE bulk_load_patients(p_list IN patient_table) IS
    BEGIN
        FORALL i IN 1 .. p_list.COUNT
            INSERT INTO patients(patient_id, name, age, gender, admitted)
            VALUES (p_list(i).patient_id, p_list(i).name, p_list(i).age, p_list(i).gender, p_list(i).admitted);
        COMMIT;
    END bulk_load_patients;

    FUNCTION show_all_patients RETURN SYS_REFCURSOR IS
        patient_cursor SYS_REFCURSOR;
    BEGIN
        OPEN patient_cursor FOR
            SELECT patient_id, name, age, gender, admitted FROM patients;
        RETURN patient_cursor;
    END show_all_patients;

    FUNCTION count_admitted RETURN NUMBER IS
        admitted_count NUMBER;
    BEGIN
        SELECT COUNT(*) INTO admitted_count
        FROM patients
        WHERE admitted = 'Y';
        RETURN admitted_count;
    END count_admitted;

    PROCEDURE admit_patient(p_id IN NUMBER) IS
    BEGIN
        UPDATE patients
        SET admitted = 'Y'
        WHERE patient_id = p_id;
        COMMIT;
    END admit_patient;

END hospital_pkg;
/
-- Bulk load patients
DECLARE
    patient_list hospital_pkg.patient_table := hospital_pkg.patient_table();
BEGIN
    patient_list.EXTEND(3);

    patient_list(1) := hospital_pkg.patient_rec(1, 'John Doe', 30, 'M', 'N');
    patient_list(2) := hospital_pkg.patient_rec(2, 'Jane Smith', 25, 'F', 'N');
    patient_list(3) := hospital_pkg.patient_rec(3, 'Alice Brown', 40, 'F', 'N');

    hospital_pkg.bulk_load_patients(patient_list);
END;
/

-- Display all patients
DECLARE
    c SYS_REFCURSOR;
    v_id patients.patient_id%TYPE;
    v_name patients.name%TYPE;
    v_age patients.age%TYPE;
    v_gender patients.gender%TYPE;
    v_admitted patients.admitted%TYPE;
BEGIN
    c := hospital_pkg.show_all_patients;
    LOOP
        FETCH c INTO v_id, v_name, v_age, v_gender, v_admitted;
        EXIT WHEN c%NOTFOUND;
        DBMS_OUTPUT.PUT_LINE(v_id || ' | ' || v_name || ' | ' || v_age || ' | ' || v_gender || ' | ' || v_admitted);
    END LOOP;
    CLOSE c;
END;
/

-- Admit a patient and count admitted
BEGIN
    hospital_pkg.admit_patient(2);
    DBMS_OUTPUT.PUT_LINE('Admitted Patients: ' || hospital_pkg.count_admitted);
END;
/
